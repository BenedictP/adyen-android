/*
 * Copyright (c) 2019 Adyen N.V.
 *
 * This file is open source and available under the MIT license. See the LICENSE file for more info.
 *
 * Created by ran on 6/2/2019.
 */
apply plugin: "maven-publish"
apply plugin: "signing"

apply from: "${rootDir}/config/gradle/artifacts.gradle"

final theGroupId = "com.adyen.checkout"
final theArtifactId = project.mavenArtifactId
final theVersion = "4.13.6-FORK-GYG-V1"

final theName = project.mavenArtifactName
final theDescription = project.mavenArtifactDescription
final theUrl = "https://github.com/Adyen/adyen-android"

final theLicenseName = "MIT License"
final theLicenseUrl = "https://opensource.org/licenses/MIT"

final theOrganizationName = "Adyen N.V."
final theOrganizationUrl = "https://www.adyen.com/"

final theTeamName = "Checkout"

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId theGroupId
                artifactId theArtifactId
                version theVersion

                artifact androidSourcesJar
                //artifact javadocJar
                artifact bundleReleaseAar

                pom {
                    name = theName
                    description = theDescription
                    url = theUrl
                    licenses {
                        license {
                            name = theLicenseName
                            url = theLicenseUrl
                        }
                    }
                    organization {
                        name = theOrganizationName
                        url = theOrganizationUrl
                    }
                    developers {
                        developer {
                            name = theTeamName
                            organization = theOrganizationName
                            organizationUrl = theOrganizationUrl
                        }
                    }
                }

                // Add dependencies to the POM file.
                pom.withXml {
                    logger.lifecycle("\nPublishing maven repository $theGroupId:$theArtifactId:$theVersion\n")

                    final dependenciesNode = asNode().appendNode("dependencies")

                    ext.addDependency = { dep, scope ->
                        if (dep.name != 'unspecified') {
                            final depGroup = dep.group != rootProject.name ? dep.group : theGroupId
                            final depName = dep.name
                            final depVersion = dep.version != 'unspecified' ? dep.version : theVersion

                            final dependencyNode = dependenciesNode.appendNode("dependency")
                            dependencyNode.appendNode("groupId", depGroup)
                            dependencyNode.appendNode("artifactId", depName)
                            dependencyNode.appendNode("version", depVersion)
                            dependencyNode.appendNode("scope", scope)

                            if (!dep.hasProperty("transitive") || !dep.transitive) {
                                final exclusionNode = dependencyNode.appendNode("exclusions").appendNode("exclusion")
                                exclusionNode.appendNode("groupId", "*")
                                exclusionNode.appendNode("artifactId", "*")
                            } else if (!dep.properties.excludeRules.empty) {
                                final exclusionNode = dependencyNode.appendNode("exclusions").appendNode("exclusion")
                                dep.properties.excludeRules.each { rule ->
                                    exclusionNode.appendNode("groupId", rule.group ?: "*")
                                    exclusionNode.appendNode("artifactId", rule.module ?: "*")
                                }
                            }
                        }
                    }

                    logger.lifecycle("- Append api dependencies:")
                    configurations.api.dependencies.each { dep -> addDependency(dep, "compile") }
                    logger.lifecycle("- Append implementation dependencies:")
                    configurations.implementation.dependencies.each { dep -> addDependency(dep, "runtime") }
                }
            }
        }

        repositories {
            maven {
                name = "gygcodeartifact"
                url = "https://getyourguide-130607246975.d.codeartifact.eu-central-1.amazonaws.com/maven/private-store/"
                credentials {
                    username "aws"
                    password System.getenv()["CODEARTIFACT_AUTH_TOKEN"]
                }
            }
        }
    }
}
